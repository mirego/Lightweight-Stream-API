plugins {
    id 'java-library'
    id 'me.champeau.gradle.jmh'
    id 'mirego.publish' version '1.0'
    id 'mirego.release' version '1.10'
    id 'mirego.j2objc' version '1.49'
}

archivesBaseName = 'stream'
group = 'com.mirego.annimon'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

repositories {
    mavenLocal()
    jcenter()
    delegate.miregoPublic()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation project(':streamTest')
}

cobertura {
    coverageExcludes = ['.*com\\.annimon\\.stream\\.internal\\.Compat.*']
}

jmh {
    benchmarkMode = ['Throughput']
    fork = 2
    threads = 2
    warmupIterations = 3
    iterations = 5
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

j2objc {
    version = '1.0.0.2'

    podName = 'lightweight-stream-api'
    prefixes = [
            'com.annimon.stream.*': ''
    ]
    incremental = false
    lintAllowWarnings = true
}

release {
    checkTasks = ['check']
    buildTasks = ['publish', 'j2objc_repo_push_quick']

    updateVersionPart = 2

    tagPrefix = 'lightweight-stream-'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact j2objc_zip
        }
    }
}