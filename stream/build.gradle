plugins {
    id 'java-library'
    id 'me.champeau.gradle.jmh'
    id 'mirego.publish' version '1.0'
    id 'mirego.release' version '1.13'
    id 'mirego.j2objc' version '1.59'
}

archivesBaseName = 'stream'
group = 'com.mirego.annimon'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

repositories {
    mavenLocal()
    jcenter()
    delegate.miregoPublic()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation project(':streamTest')
}

cobertura {
    coverageExcludes = ['.*com\\.annimon\\.stream\\.internal\\.Compat.*']
}

jmh {
    benchmarkMode = ['Throughput']
    fork = 2
    threads = 2
    warmupIterations = 3
    iterations = 5
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

j2objc {
    version = '1.0.0.2'

    podName = 'lightweight-stream-api'
    podspecFile = "../${j2objc.podName}.podspec"

    generatedDir = "../${project.name}-j2objc"
    generatedTargetDir = "${project.name}-j2objc"

    prefixes = [
            'com.annimon.stream.*': 'CAS'
    ]

    options = '-encoding UTF-8 -Werror --build-closure --doc-comments --generate-deprecated -J-Xmx2G --no-package-directories --nullability --swift-friendly --header-mapping header-mapping.j2objc'
}

tasks['j2objc'].doLast {
    // Some hacking:
    // The following files generate header file names that clash with jre-emul after transpilling
    // Renaming header and source files to CAS*.h and CAS*.m and the --header-mapping replace the #include references
    renameHeader('Optional', 'CAS') // Optional.java
    renameHeader('Function', 'CAS') // Function.java
    renameHeader('Objects', 'CAS') // Objects.java
    renameHeader('Predicate', 'CAS') // Predicate.java
    renameHeader('Supplier', 'CAS') // Supplier.java
    renameHeader('Stream', 'CAS') // Stream.java
}

def renameHeader(fileName, prefix) {
    def genDir = file(project.extensions['j2objc'].generatedDir)
    new File(genDir, fileName +'.h').renameTo(new File(genDir, prefix + fileName + '.h'))
    def mFile = new File(genDir, fileName + '.m')
    mFile.text = mFile.text.replaceAll('#include "' + fileName + '.h"', '#include "' + prefix + fileName + '.h"')
    mFile.renameTo(new File(genDir, prefix + fileName+ '.m'))
}

release {
    checkTasks = ['check']
    buildTasks = ['publish', 'j2objc_repo_push_quick']

    updateVersionPart = 2

    tagPrefix = 'lightweight-stream-'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact j2objc_zip
        }
    }
}
