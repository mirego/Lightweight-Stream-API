apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'mirego.publish'
apply plugin: 'mirego.release'

archivesBaseName = 'stream'
group = 'com.annimon'
ext.isReleaseVersion = !version.contains("SNAPSHOT")

sourceCompatibility = '1.7'
targetCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

repositories {
    mavenLocal()
    jcenter()
    delegate.miregoPublic()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation project(':streamTest')
}

cobertura {
    coverageExcludes = ['.*com\\.annimon\\.stream\\.internal\\.Compat.*']
}

jmh {
    benchmarkMode = ['Throughput']
    fork = 2
    threads = 2
    warmupIterations = 3
    iterations = 5
}

// maven signing
if (ext.isReleaseVersion) {
    apply from: 'signing.gradle'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

release {
    checkTasks = ['check']
    buildTasks = ['publish']

    updateVersionPart = 2

    tagPrefix = 'lightweight-stream-'
}